configurations {
    runtime
    archives
}

dependencies {
    runtime project(':spark-osmpbf')
}

task syncLibs(type: Sync) {
    into "lib/default"
    from configurations.runtime
}

def artefactPrefix = 'spark-osmpbf-' + version

task distZip(type: Zip) {
    description = 'Builds a zip file containing a self-contained distribution of the application.'
    archiveFileName = artefactPrefix + '.zip'
    destinationDirectory = new File(projectDir, 'build/distribution')
    from('.') {
    	exclude 'build*'
    	exclude 'ivy.xml'
    	exclude '.*'
    }
}
distZip.dependsOn syncLibs

task distTgz(type: Tar) {
    description = 'Builds a tgz file containing a self-contained distribution of the application.'
    archiveFileName = artefactPrefix + '.tgz'
    destinationDirectory = new File(projectDir, 'build/distribution')
    compression = Compression.GZIP
    from('.') {
    	exclude 'build*'
    	exclude 'ivy.xml'
    	exclude '.*'
    }
}
distTgz.dependsOn syncLibs

task assemble {
    description = 'Generates a working application directory structure.'
}
assemble.dependsOn distZip, distTgz

task build {
	description = 'Assembles and tests this project.'
}
build.dependsOn assemble

task clean(type: Delete) {
    delete 'build'
}

artifacts {
    archives distZip, distTgz
}
